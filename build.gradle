plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.ec4j.editorconfig' version "0.1.0"
    id "org.sonarqube" version "6.0.1.5171"
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.TnT'
version = '0.0.1-SNAPSHOT'
jar.enabled = false

java {
    sourceCompatibility = '21'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


sonar {
    properties {
        property "sonar.projectKey", "YAPP-Github_25th-App-Team-2-BE"
        property "sonar.organization", "yapp-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**/*Application*.java, **/*Config*.java, **/*GlobalExceptionHandler.java, **/Q*.java, **/DynamicQuery.java, " +
                "**/*Exception.java"
        property "sonar.java.coveragePlugin", "jacoco"
    }
}

checkstyle {
    toolVersion = "10.4"
    maxWarnings = 0
    configFile = file("${rootDir}/config/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/naver-checkstyle-suppressions.xml"]
}

check.dependsOn editorconfigCheck

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
    }

    def Qdomains = []

    for (qPattern in '**/QA'..'**/QZ') { // qPattern = '**/QA', '**/QB', ... '*.QZ'
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application*",
                            "**/*Config*",
                            "**/*DynamicQuery*"
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = [
                    '*.*Application',
                    '*.*Config',
                    '*.error.*',
                    '*.dto.*',
                    '*.*AppleEcdsaKeyProvider',
                    '*.Q*',
                    '*.DynamicQuery'
            ]
        }
    }
}

editorconfig {
    excludes = ['build']
}

ext {
    queryDslVersion = "6.10.1"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // TSID
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.9.0'

    // 애플 로그인 관련 라이브러리
    implementation 'com.auth0:java-jwt:4.4.0'
    implementation 'com.auth0:jwks-rsa:0.22.1'
    implementation 'org.json:json:20231013'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.79'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Log4j2
    // -DLog4jContextSelector=org.apache.logging.log4j.core.async.AsyncLoggerContextSelector -> VM for Async Logger
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'
    implementation 'com.lmax:disruptor:4.0.0'

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // MockWebServer
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // Querydsl
    implementation("io.github.openfeign.querydsl:querydsl-core:$queryDslVersion")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")
    annotationProcessor("jakarta.annotation:jakarta.annotation-api")
    annotationProcessor("jakarta.persistence:jakarta.persistence-api")

    // AWS S3 SDK
    implementation 'software.amazon.awssdk:s3:2.30.0'

    // TwelveMonkeys - 다양한 이미지 포맷 지원
    implementation 'com.twelvemonkeys.imageio:imageio-core:3.12.0'
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.12.0'  // JPEG 지원
    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.12.0'  // WebP 지원 (Android)

    // Thumbnailator - 이미지 리사이징과 포맷 변환
    implementation 'net.coobird:thumbnailator:0.4.20'
}
